plugins {
  id 'java'
  id 'org.springframework.boot' version '3.4.5'
  id 'io.spring.dependency-management' version '1.1.7'
  id "org.sonarqube" version "6.0.1.5171"
  id "com.diffplug.spotless" version "7.0.3"
  id 'jacoco'
}

group = 'com.dyno'
version = '1.9.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  compileOnly 'org.projectlombok:lombok'

  runtimeOnly 'com.h2database:h2'
  runtimeOnly 'org.postgresql:postgresql'

  annotationProcessor 'org.projectlombok:lombok'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
  useJUnitPlatform()
}

tasks.register('initGitHooks', Copy) {
  from("$rootDir/.githooks") {
    include 'pre-commit'
    rename 'pre-commit', 'pre-commit'
  }
  into "$rootDir/.git/hooks"

  doLast {
    def hookFile = file("$rootDir/.git/hooks/pre-commit")
    if (hookFile.exists()) {
      hookFile.setExecutable(true)
      println "✅ pre-commit hook 설치 완료: ${hookFile.path}"
    } else {
      println "❌ pre-commit hook 파일이 존재하지 않습니다."
    }
  }
}

tasks.named("clean") {
  dependsOn("initGitHooks")
}


spotless {
  java {
    target 'src/**/*.java'
    // 포맷터 설정: Google Java Format or Eclipse
    googleJavaFormat('1.27.0')
    // 불필요한 import 제거
    removeUnusedImports()
    // import 순서 정리 (기본: 알파벳 순)
    importOrder()
    // 기타 포맷 설정
    trimTrailingWhitespace()
    endWithNewline()
  }

  format 'misc', {
    target '*.md', '*.gradle', '*.yml', '*.yaml'
    trimTrailingWhitespace()
    endWithNewline()
  }
}

sonar {
  properties {
    property "sonar.projectKey", "dyno-jun_spring-server-starter"
    property "sonar.organization", "dyno-jun"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

jacoco {
  toolVersion = "0.8.11"
}

jacocoTestReport {
  dependsOn test

  reports {
    xml.required = true
    html.required = true
    csv.required = false
  }
}
